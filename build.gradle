import com.modrinth.minotaur.TaskModrinthUpload
import io.papermc.hangarpublishplugin.model.Platforms
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'com.modrinth.minotaur' version '2.+'
    id 'io.papermc.hangar-publish-plugin' version '0.0.5'
}

def buildNum = System.getenv('GITHUB_RUN_NUMBER') ?: 'SNAPSHOT'
project.group = 'net.pl3x.map'
project.version = "$minecraftVersion-$buildNum"

def npmCmd = DefaultNativePlatform.currentOperatingSystem.isWindows() ? 'npm.cmd' : 'npm'

dependencies {
    compileOnly project(':core')
    compileOnly project(':bukkit')
    compileOnly project(':fabric')
    compileOnly project(':forge')
}

defaultTasks 'build'

def cleanWebmap = tasks.register('cleanWebmap', Delete) {
    dependsOn clean
    println 'Cleaning webmap...'
    delete "$rootDir/core/src/main/resources/web"
}

def combineJars = tasks.register('combineJars', Jar) {
    mustRunAfter build
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(files(subprojects.findAll { it.name != 'webmap' }.collect {
        it.layout.buildDirectory.file("libs/${rootProject.name}-${it.name}-${it.version}.jar").get()
    }).filter { it.name != 'MANIFEST.MF' }.collect { if (it.isDirectory()) it else zipTree(it) })
    manifest {
        // this must be here because it overrides the default jar task
        attributes['Main-Class'] = "${rootProject.group}.core.Pl3xMap"
    }
}

def npmInstall = tasks.register('npmInstall', Exec) {
    println 'Installing npm dependencies...'
    workingDir new File(projectDir, 'webmap')
    commandLine npmCmd, 'install'
}

def npmBuild = tasks.register('npmBuild', Exec) {
    dependsOn npmInstall
    println 'Building webmap...'
    workingDir new File(projectDir, 'webmap')
    commandLine npmCmd, 'run', 'build'
}

def copyWebmap = tasks.register('copyWebmap', Copy) {
    dependsOn npmBuild
    println 'Copying webmap...'
    from "$rootDir/webmap/public"
    include '**/*'
    exclude 'tiles*/'
    into "$rootDir/core/src/main/resources/web"
    from "$rootDir/webmap/dist"
    include '**/*'
    into "$rootDir/core/src/main/resources/web"
}

assemble {
    // copy the webmap over
    dependsOn copyWebmap
    mustRunAfter copyWebmap
    // this is to ensure the subprojects finish building completely before this task is finished
    subprojects
            .findAll { it.name != 'webmap' }
            .forEach { project ->
                dependsOn ":${project.name}:build"
            }
    // after subprojects are finished we can combine their jars into a fatjar
    finalizedBy combineJars
}

clean {
    finalizedBy cleanWebmap
}

allprojects {
    if (name == 'webmap') {
        return
    }

    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        implementation "cloud.commandframework:cloud-core:$cloudVersion"
        implementation "cloud.commandframework:cloud-brigadier:$cloudVersion"
        implementation "cloud.commandframework:cloud-paper:$cloudVersion"
        implementation("cloud.commandframework:cloud-minecraft-extras:$cloudVersion") {
            exclude group: 'net.kyori', module: '*'
        }

        implementation "net.kyori:adventure-api:$adventureVersion"
        implementation "net.kyori:adventure-text-minimessage:$adventureVersion"
        implementation "net.kyori:adventure-text-serializer-plain:$adventureVersion"
        implementation "net.kyori:adventure-platform-bukkit:$adventureBukkitVersion"

        implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
        implementation "com.github.Querz:NBT:$querzNbtVersion"
        implementation("com.github.Carleslc.Simple-YAML:Simple-Yaml:$simpleYamlVersion") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        implementation "io.undertow:undertow-core:$undertowVersion"
        implementation "org.jboss.xnio:xnio-nio:$xnioVersion"

        // provided by mojang
        compileOnly "com.google.code.gson:gson:$gsonVersion"
        compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
    }

    jar {
        if (rootProject.name == project.name) {
            archiveBaseName = rootProject.name
        } else {
            archiveBaseName = "${rootProject.name}-${project.name}"
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.release.set(17)
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    processResources {
        filteringCharset = 'UTF-8'
    }
}

var file = rootProject.layout.buildDirectory.file("libs/${rootProject.name}-${project.version}.jar")

modrinth {
    autoAddDependsOn = false
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'pl3xmap'
    versionName = "${project.version}"
    versionNumber = "${project.version}"
    versionType = 'alpha'
    uploadFile = file.get()
    gameVersions.addAll(["${minecraftVersion}"])
    loaders.addAll(['bukkit', 'spigot', 'paper', 'purpur', 'fabric', 'forge'])
    changelog = System.getenv('COMMIT_MESSAGE')
}

hangarPublish {
    var mcVer = "${minecraftVersion}"
    var hangarToken = System.getenv('HANGAR_TOKEN')
    var commitMessage = System.getenv('COMMIT_MESSAGE')
    publications.register("plugin") {
        version.set(project.version as String)
        namespace('BillyGalbreath', 'Pl3xMap')
        channel.set('Alpha')
        changelog.set(commitMessage)
        apiKey.set(hangarToken)
        platforms {
            register(Platforms.PAPER) {
                jar.set(file.get())
                platformVersions.set([mcVer])
            }
        }
    }
}

class ModrinthNoBuildPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.tasks.register('modrinth_nobuild', TaskModrinthUpload.class, task -> {
            task.setGroup("publishing")
            task.setDescription("Upload project to Modrinth")
            //task.dependsOn(tasks.named("assemble"))
            //task.mustRunAfter(tasks.named("build"))
        })
    }
}

apply plugin: ModrinthNoBuildPlugin
